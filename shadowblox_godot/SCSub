# -*- mode: python -*-

Import(
    [
        "env_base",
        "luau_lib",
        "luau_includes",
        "shadowblox_lib",
        "shadowblox_includes",
    ]
)

env = env_base.Clone()
env["use_llvm"] = True
env["use_hot_reload"] = False
Export("env")

# Build godot-cpp first to get the configured environment
env = SConscript("../extern/godot-cpp/SConstruct")

# Add our include paths
env.Append(CPPPATH=["src/", "include/"])
sources = Glob("src/*.cpp")

# Link shadowblox and luau libraries
env.Append(LIBS=[shadowblox_lib, luau_lib])
env.Append(CPPPATH=luau_includes)
env.Append(CPPPATH=shadowblox_includes)

if env["target"] in ["editor", "template_debug"]:
    doc_data = env.GodotCPPDocData(
        "src/gen/doc_data.gen.cpp", source=Glob("doc_classes/*.xml")
    )
    sources.append(doc_data)

if env["platform"] == "macos":
    library = env.SharedLibrary(
        "bin/shadowblox/libshadowbloxgd.{}.{}.framework/libshadowbloxgd.{}.{}".format(
            env["platform"], env["target"], env["platform"], env["target"]
        ),
        source=sources,
    )
elif env["platform"] == "ios":
    if env["ios_simulator"]:
        library = env.StaticLibrary(
            "bin/shadowblox/libshadowbloxgd.{}.{}.simulator.a".format(
                env["platform"], env["target"]
            ),
            source=sources,
        )
    else:
        library = env.StaticLibrary(
            "bin/shadowblox/libshadowbloxgd.{}.{}.a".format(
                env["platform"], env["target"]
            ),
            source=sources,
        )
else:
    library = env.SharedLibrary(
        "bin/shadowblox/libshadowbloxgd{}{}".format(env["suffix"], env["SHLIBSUFFIX"]),
        source=sources,
    )

env.NoCache(library)
Default(library)
